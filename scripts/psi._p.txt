
model {

# Priors
for(k in 1:nspec){  # Group lpsi and lp together in array eta
   lpsi[k] <- eta[k,1]
   lp[k] <- eta[k,2]
   eta[k, 1:2] ~ dmnorm(mu.eta[], Omega[,])
}
# Hyperpriors
# Priors for mu.lpsi=mu.eta[1] and mu.lp=mu.eta[2]
# probs = community means of occupancy and detection probability
for(v in 1:2){
   mu.eta[v] <- log(probs[v] / (1-probs[v]))
   probs[v] ~ dunif(0,1)
}
# Prior for variance-covariance matrix
Omega[1:2, 1:2] ~ dwish(R[,], df)
Sigma[1:2, 1:2] <- inverse(Omega[,])

# Ecological model for latent occurrence z (process model)
for(k in 1:nspec){
   logit(psi[k]) <- lpsi[k]   # Must take outside of i loop
   for (i in 1:M) {
      z[i,k] ~ dbern(psi[k])
   }
}

# Observation model for observed data y
for(k in 1:nspec){
   logit(p[k]) <- lp[k]       # Needs to be outside of i loop
   for (i in 1:M) {
      mu.p[i,k] <- z[i,k] * p[k]
      ysum[i,k] ~ dbin(mu.p[i,k], J[i])
   }
}

# Derived quantities
rho <- Sigma[1,2] / sqrt(Sigma[1,1] * Sigma[2,2])  # Correlation coefficient
for(k in 1:nspec){
   Nocc.fs[k] <- sum(z[,k])   # Number of occupied sites among the 21
}
for (i in 1:M) {
   Nsite[i] <- sum(z[i,])     # Number of occurring species
}
}

