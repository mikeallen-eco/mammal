
model {

# Priors
for(k in 1:nspec){          # Loop over species
   psi[k] ~ dunif(0, 1)
   p[k] ~ dunif(0, 1)
}

# Ecological model for latent occurrence z (process model)
for(k in 1:nspec){          # Loop over species
   for (i in 1:M) {         # Loop over sites
      z[i,k] ~ dbern(psi[k])
   }
}

# Observation model for observed data y
for(k in 1:nspec){          # Loop over species
   for (i in 1:M) {
      mup[i,k] <- z[i,k] * p[k]
      ysum[i,k] ~ dbin(mup[i,k], J[i])
   }
}

# Derived quantities
for(k in 1:nspec){          # Loop over species
   Nocc.fs[k] <- sum(z[,k]) # Add up number of occupied sites among the 267
}
for (i in 1:M) {            # Loop over sites
   Nsite[i] <- sum(z[i,])   # Add up number of occurring species at each site
}
}

